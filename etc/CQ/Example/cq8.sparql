# CQ 8: Which investigational medicinal products are related to this manufactured item?

prefix cmns-col:    <https://www.omg.org/spec/Commons/Collections/>
prefix cmns-dsg:    <https://www.omg.org/spec/Commons/Designators/>
prefix cmns-pts:    <https://www.omg.org/spec/Commons/PartiesAndSituations/>
prefix idmp-mprd:   <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11615-MedicinalProducts/>
prefix idmp-sub:    <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>
prefix rdfs:        <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT distinct ?investigationalMedicinalProduct

WHERE {
	# Bind the name we want into a variable 
	BIND("Norvasc" AS ?manufacturedItemLabel )
	
	# Find Manufactured Items
	?manufacturedItemType rdfs:subClassOf* idmp-sub:ManufacturedItem .
	?manufacturedItem rdf:type ?manufacturedItemType .
	?manufacturedItem rdfs:label ?manufacturedItemLabel .
	
	# Find products that include the manufactured item based on their composition
	?manufacturedItem cmns-dsg:isDefinedIn ?productComposition .
	?productComposition cmns-dsg:defines ?pharmaceuticalProduct .
	?pharmaceuticalProduct cmns-col:isIncludedIn ?medicinalProduct .

  	# Find Manufactured Items and that have the role of Investigational Medicinal product
    ?investigationalMedicinalProduct cmns-pts:isPlayedBy ?medicinalProduct .
    ?investigationalMedicinalProduct rdf:type idmp-mprd:InvestigationalMedicinalProduct .
}